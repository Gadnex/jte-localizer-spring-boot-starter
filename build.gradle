import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'java'
    id 'maven-publish'
    id 'org.springframework.boot' version '3.4.1' apply false
    id 'io.spring.dependency-management' version '1.1.7'
    id 'pl.allegro.tech.build.axion-release' version '1.18.16'
    id 'com.diffplug.spotless' version '7.0.0.BETA4'
}

group = 'io.github.gadnex'
version = scmVersion.version

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom SpringBootPlugin.BOM_COORDINATES
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'gg.jte:jte:3.1.16'
    annotationProcessor 'org.springframework.boot:spring-boot-autoconfigure-processor'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'jte-localizer-spring-boot-starter'
            from components.java
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs.add("-parameters")
}

tasks.withType(Jar) {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

scmVersion {
    tag {
        prefix.set("")
    }
}

spotless {
    java {
        googleJavaFormat()
    }
}